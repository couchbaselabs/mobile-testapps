plugins {
    id 'java'
}

group 'com.couchbase.mobiletestkit'

ext {
    DEF_VERSION = '3.0.0-38'
    COUCHBASE_LITE_JAVA_VERSION = System.getProperty('version', DEF_VERSION)
    VECTOR_SEARCH_JAVA_VERSION = System.getenv('VECTOR_SEARCH_VERSION')

    JAVA_COMMON_DIR = "${projectDir}/../JavaRequestHandlers"
    ASSET_DIR = "${JAVA_COMMON_DIR}/src/main/assets"
    JAVA_SERVER_DIR = "${projectDir}/../JavaRequestListener"
}

println "BUILDING TEST SERVER WITH CBL VERSION ${COUCHBASE_LITE_JAVA_VERSION}"
println "BUILDING TEST SERVER WITH VS VERSION ${VECTOR_SEARCH_JAVA_VERSION}"

sourceCompatibility = 1.8

jar {
    archiveBaseName = 'CBLTestServer-Java'
    archiveAppendix = 'Desktop'
    archiveVersion = COUCHBASE_LITE_JAVA_VERSION
    archiveClassifier = 'enterprise'

    manifest {
        attributes('Main-Class': 'com.couchbase.mobiletestkit.javatestserver.TestServerMain')
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

repositories {
    mavenCentral()
    maven {
        url 'http://proget.build.couchbase.com/maven2/internalmaven/'
        allowInsecureProtocol = true
        // Release repo
        // url 'https://mobile.maven.couchbase.com/maven2/dev/'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

java {
    sourceSets {
        main {
            java.srcDirs = [
                    'src/main/java',
                    "${JAVA_COMMON_DIR}/src/main/java",
                    "${JAVA_SERVER_DIR}/src/main/java"
            ]
            resources.srcDirs = [ASSET_DIR]
        }
    }
}

dependencies {
    implementation 'commons-daemon:commons-daemon:1.2.2'
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    implementation "com.couchbase.lite:couchbase-lite-java-ee:${COUCHBASE_LITE_JAVA_VERSION}"
    implementation "com.couchbase.lite:couchbase-lite-java-vector-search:${VECTOR_SEARCH_JAVA_VERSION}"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.nanohttpd:nanohttpd:2.3.2-SNAPSHOT'
}
